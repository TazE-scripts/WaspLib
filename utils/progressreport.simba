(*
# Progress Report
Formatted progress reports with bordered table output.
*)
{$DEFINE WL_PROGRESS_REPORT_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils/time.simba}

(*
## TProgressReport
```pascal
TProgressReport = record
  TimeRunning: TStopwatch;
  IsSetup: Boolean;
  Headers: TStringArray;
  ValueProvider: function: TStringArray of Object;
  Title: String;
  PrintTimer: TCountDown;
end;
```
Progress report record for managing formatted table output with timing and print frequency control.
*)
type
  TProgressReport = record
    TimeRunning: TStopwatch;
    IsSetup: Boolean;
    Headers: TStringArray;
    ValueProvider: function: TStringArray of Object;
    Title: String;
    PrintTimer: TCountDown;
  end;

(*
## TProgressReport.Setup
```pascal
procedure TProgressReport.Setup(title: String = 'Progress Report'; headers: TStringArray = []);
```
Sets up the progress report with title and column headers.
*)
procedure TProgressReport.Setup(title: String = 'Progress Report'; headers: TStringArray = []; printInterval: Integer = ONE_MINUTE * 5);
begin
  Self.IsSetup := True;
  Self.TimeRunning.Start();
  Self.Headers := headers;
  Self.Title := title;
  Self.PrintTimer.Start(printInterval);
end;

(*
## TProgressReport.GenerateReport
```pascal
function TProgressReport.GenerateReport(values: TStringArray): String;
```
Generates a formatted report with the provided values in a bordered table format.
*)
function TProgressReport.GenerateReport(values: TStringArray): String;
var
  i, totalWidth: Integer;
  half: Single;
  str, lines, btm, header: String;
const
  COLUMN_WIDTH = 15;
  SEPARATOR_WIDTH = 5;
begin
  if Length(Self.Headers) <> Length(values) then
    raise 'TProgressReport.GenerateReport: Header count (' + 
          IntToStr(Length(Self.Headers)) + ') must match value count (' + 
          IntToStr(Length(values)) + ')';
          
  totalWidth := COLUMN_WIDTH * 2 + SEPARATOR_WIDTH;
  
  for i := 0 to High(Self.Headers) do
  begin
    str := '| ' + Self.Headers[i].PadRight(COLUMN_WIDTH) + ' ' + 
           values[i].PadRight(COLUMN_WIDTH) + ' |';
    lines += str + LINE_SEP;
  end;
  
  btm := '+' + '-' * (totalWidth-2) + '+';
  header := '+ ' + Self.Title + ' ' + '-' * (totalWidth - Length(Self.Title) - 4) + '+';
  
  Result := header + LINE_SEP + lines + btm;
end;

(*
## TProgressReport.PrintReport
```pascal
procedure TProgressReport.PrintReport();
```
Displays the current report using the ValueProvider function to retrieve current values.
*)
procedure TProgressReport.PrintReport();
begin
  if Self.PrintTimer.IsFinished then
  begin
    WriteLn Self.GenerateReport(Self.ValueProvider());
    Self.PrintTimer.Restart();
  end;
end;
